// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Add16 (a=regout, b[0]=inc, out=incval);
    Mux16 (a=in, b=false, sel=reset, out=resetin);
    Or (a=reset, b=load, out=resetorload);
    Mux16 (a=incval, b=resetin, sel=resetorload, out=val);
    Or (a=resetorload, b=inc, out=update);
    Register (in=val, load=update, out=regout, out=out);
}
